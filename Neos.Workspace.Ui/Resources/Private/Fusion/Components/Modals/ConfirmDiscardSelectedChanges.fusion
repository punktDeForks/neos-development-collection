prototype(Neos.Workspace.Ui:Component.Modal.ConfirmDiscardSelectedChanges) < prototype(Neos.Fusion:Component) {
  workspaceName=''
  workspaceTitle=''

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    popoverId = 'confirm-discard-selected-changes-popover'

    confirmDiscardSelectedChanges = Neos.Fusion:ActionUri {
      action = 'publishOrDiscardNodesAction'
      format = 'htmx'
      arguments {
        selectedWorkspace = ${props.workspaceName}
        action = 'discard'
      }
    }
  }

  renderer = afx`
    <div popover id={private.popoverId} hx-indicator="#indicator">
      <header>
        <button
          type="button"
          class="neos-close neos-button"
          popovertarget={private.popoverId}
          popovertargetaction="close"
        >
          <Neos.Workspace.Ui:Component.Icon icon="times"/>
        </button>
        <div class="neos-header">
          {private.i18n.id('workspaces.createWorkspaceRoleAssignment').arguments([props.workspaceTitle])}
        </div>
      </header>
      <section>
        <Neos.Workspace.Ui:Component.Button
          label={"cancel"}
          attributes.popovertarget={private.popoverId}
          attributes.popovertargetaction="close"
        />
        <Neos.Workspace.Ui:Component.Button
          isDanger
          label={"delete"}
          icon="trash-alt icon-white"
          attributes.hx-target={'#popover-container'}
          attributes.hx-swap='innerHTML'
          attributes.hx-get={private.confirmDiscardSelectedChanges}
        />
      </section>
    </div>
  `
}
